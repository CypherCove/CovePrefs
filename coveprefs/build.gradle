plugins {
    id 'com.android.library'
    id 'maven-publish'
}
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        archiveClassifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        failOnError = false
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        from javadoc.destinationDir
        archiveClassifier = 'javadoc'
    }
}

dependencies {
    api "androidx.appcompat:appcompat:${rootProject.ext.appCompatVersion}"
    api "androidx.preference:preference:${rootProject.ext.preferenceVersion}"
    api "androidx.annotation:annotation:${rootProject.ext.annotationVersion}"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.13.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifactId = artifactId
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = 'CovePrefs'
                    description = 'A variety of Preferences for Android based on androidx.preference.'
                    url = 'https://github.com/CypherCove/CovePrefs'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    organization {
                        name = 'Cypher Cove'
                        url = 'www.cyphercove.com'
                    }
                    scm {
                        connection = 'scm:git:git@github.com:cyphercove/coveprefs.git'
                        developerConnection = 'scm:git:git@cyphercove/coveprefs.git'
                        url = 'https://github.com/cyphercove/CovePrefs/'
                    }
                }
            }
        }
    }
}
plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}
android {
    namespace "com.cyphercove.coveprefs"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.preference:preference:1.2.1"
    implementation "androidx.annotation:annotation:1.6.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.13.2'
}

publishing {
    publications {
        release(MavenPublication) {
            artifactId = "coveprefs"
            afterEvaluate {
                from(components["release"])
            }
            pom {
                name = 'CovePrefs'
                description = 'A variety of Preferences for Android based on androidx.preference.'
                url = 'https://github.com/CypherCove/CovePrefs'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                organization {
                    name = 'Cypher Cove'
                    url = 'www.cyphercove.com'
                }
                developers {
                    developer {
                        id = 'cypher-cove'
                        name = 'Cypher Cove'
                        email = 'contact@cyphercove.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:cyphercove/coveprefs.git'
                    developerConnection = 'scm:git:git@cyphercove/coveprefs.git'
                    url = 'https://github.com/cyphercove/CovePrefs/'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("SONATYPE_USERNAME")
                password = System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.release
}